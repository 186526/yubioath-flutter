def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new FileNotFoundException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: "org.jetbrains.kotlin.plugin.serialization"
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

android {
    signingConfigs {
        release {

            // Recreate keystore file
            String keystoreFileName = 'yubioath-android.jks'
            File keystoreDir = new File('app');
            if (!keystoreDir.exists()) {
                mkdir(keystoreDir)
            }
            File keystoreFile = new File('app/' + keystoreFileName)
            byte[] keystoreData = System.getenv('YUBIOATH_STORE_BASE64').decodeBase64()
            FileOutputStream keystoreFileOutputStream = new FileOutputStream(keystoreFile)
            try {
                keystoreFileOutputStream.write(keystoreData)
            } finally {
                keystoreFileOutputStream.close()
            }

            // set signing properties
            storeFile file(keystoreFileName)
            storePassword System.getenv('YUBIOATH_STORE_PASSWORD')
            keyAlias System.getenv('YUBIOATH_KEY_ALIAS')
            keyPassword System.getenv('YUBIOATH_KEY_PASSWORD')
        }
    }
    compileSdkVersion flutter.compileSdkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.yubico.yubioath"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lint {
        abortOnError false
    }

}

flutter {
    source '../..'
}

dependencies {
    api "com.yubico.yubikit:android:$project.yubiKitVersion"
    api "com.yubico.yubikit:management:$project.yubiKitVersion"
    api "com.yubico.yubikit:oath:$project.yubiKitVersion"
    api "com.yubico.yubikit:support:$project.yubiKitVersion"

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    implementation 'androidx.fragment:fragment-ktx:1.4.1'

    // testing dependencies
    testImplementation "junit:junit:$project.junitVersion"
    testImplementation "org.mockito:mockito-core:$project.mockitoVersion"
}
